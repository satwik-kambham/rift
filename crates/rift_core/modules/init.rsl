# Dashboard

dashboardId = -1
dashboardText = "
Welcome to rift!

Basic Keybinds:

esc - Quit Insert Mode
i - Enter Insert Mode
space q - Quit
space f - Fuzzy File Picker (with fd & fzf)
space F - File Explorer
space b - Switch Buffer
"

fn createDashboardBuffer() {
    if dashboardId == -1 {
        bufferId = createSpecialBuffer("Dashboard")
        setBufferContent(bufferId, dashboardText)
        setActiveBuffer(bufferId)
    }
}

createDashboardBuffer()

#################

# Agentic Chat

fn toolDeclarations() {
    return
"[
    {
        \"type\": \"function\",
        \"function\": {
            \"name\": \"run_shell_command\",
            \"description\": \"Run a shell command and return the output\",
            \"parameters\": {
                \"type\": \"object\",
                \"properties\": {
                    \"command\": {\"type\": \"string\"}
                },
                \"required\": [\"command\"]
            }
        }
    },
    {
        \"type\": \"function\",
        \"function\": {
            \"name\": \"find_file\",
            \"description\": \"Returns paths matching regex pattern by searching recursively in the workspace folder, returning absolute paths. Does not accept glob patterns.\",
            \"parameters\": {
                \"type\": \"object\",
                \"properties\": {
                    \"pattern\": {\"type\": \"string\"}
                },
                \"required\": [\"pattern\"]
            }
        }
    },
    {
        \"type\": \"function\",
        \"function\": {
            \"name\": \"search_workspace\",
            \"description\": \"Grep search in the current workspace for matching patterns\",
            \"parameters\": {
                \"type\": \"object\",
                \"properties\": {
                    \"pattern\": {\"type\": \"string\"}
                },
                \"required\": [\"pattern\"]
            }
        }
    },
    {
        \"type\": \"function\",
        \"function\": {
            \"name\": \"read_file\",
            \"description\": \"Get the content of a file with line numbers\",
            \"parameters\": {
                \"type\": \"object\",
                \"properties\": {
                    \"path\": {\"type\": \"string\"}
                },
                \"required\": [\"path\"]
            }
        }
    },
    {
        \"type\": \"function\",
        \"function\": {
            \"name\": \"write_file\",
            \"description\": \"Writes content to a specified file replacing the existing content.\",
            \"parameters\": {
                \"type\": \"object\",
                \"properties\": {
                    \"file_path\": {\"type\": \"string\"},
                    \"content\": {\"type\": \"string\"}
                },
                \"required\": [\"file_path\", \"content\"]
            }
        }
    },
    {
        \"type\": \"function\",
        \"function\": {
            \"name\": \"replace\",
            \"description\": \"Replaces text within a file. Can replace single or multiple occurrences.\",
            \"parameters\": {
                \"type\": \"object\",
                \"properties\": {
                    \"file_path\": {\"type\": \"string\"},
                    \"old_string\": {\"type\": \"string\"},
                    \"new_string\": {\"type\": \"string\"}
                },
                \"required\": [\"file_path\", \"old_string\", \"new_string\"]
            }
        }
    }
]"
}

fn agentRunShellCommand(command) {
    outputs = runShellCommand(command, getWorkspaceDir())
    return "STDOUT:\n" + tableGet(outputs, "stdout") + "\nSTDERR:\n" + tableGet(outputs, "stderr")
}

fn agentFindFile(pattern) {
    results = runShellCommand("fd --type f --strip-cwd-prefix --full-path --absolute-path " + pattern, getWorkspaceDir())
    return tableGet(results, "stdout")
}

fn agentSearchWorkspace(pattern) {
    results = runShellCommand("rg " + pattern, getWorkspaceDir())
    return tableGet(results, "stdout")
}

llmChatId = -1
chatInput = ""
chatHistory = createArray()
pendingToolCalls = createArray()

fn renderLLMChat() {
    content = ""
    i = 0
    loop {
        if i >= arrayLen(chatHistory) { break }
        message = arrayGet(chatHistory, i)
        content = content + "\n------\n" + tableGet(message, "role") + ":\n" + tableGet(message, "content")
        toolCalls = tableGet(message, "tool_calls")
        if toolCalls != null {
            content = content + "\n\n" + toJson(toolCalls)
        }
        content = content + "\n------\n"
        i = i + 1
    }
    if arrayLen(pendingToolCalls) != 0 {
        i = 0
        loop {
            if i >= arrayLen(pendingToolCalls) { break }
            toolCall = arrayGet(pendingToolCalls, i)
            content = content + "\n" + toJson(toolCall) + "\n"
            i = i + 1
        }
    }
    content = content + "\n------\n" + chatInput + "\n------\n"
    setBufferContent(llmChatId, content)
}

fn requestChatCompletion() {
    apiKey = getEnvVar("OPENROUTER_KEY")
    
    request = createTable()
    tableSet(request, "model", "mistralai/devstral-small")
    tableSet(request, "messages", chatHistory)
    tableSet(request, "tools", fromJson(toolDeclarations()))
    tableSet(request, "stream", false)
    tableSet(request, "temperature", 0.3)
    tableSet(request, "seed", 42)

    body = toJson(request)
    response = postRequestWithBearerToken("https://openrouter.ai/api/v1/chat/completions", body, apiKey)
    response = fromJson(response)
    message = tableGet(arrayGet(tableGet(response, "choices"), 0), "message")
    toolCalls = tableGet(message, "tool_calls")
    
    if toolCalls != null {
        i = arrayLen(toolCalls)
        loop {
            if i <= 0 { break }
            toolCall = arrayGet(toolCalls, i - 1)
            
            arrayPushBack(pendingToolCalls, toolCall)
            
            toolName = tableGet(tableGet(toolCall, "function"), "name")
            toolArgs = fromJson(tableGet(tableGet(toolCall, "function"), "arguments"))
            toolCallId = tableGet(toolCall, "id")
            
            i = i - 1
        }
    }
    
    arrayPushBack(chatHistory, message)

    renderLLMChat()
}

fn getToolCallResult(allowed) {
    toolCall = arrayPopBack(pendingToolCalls)
    
    toolName = tableGet(tableGet(toolCall, "function"), "name")
    toolArgs = fromJson(tableGet(tableGet(toolCall, "function"), "arguments"))
    toolCallId = tableGet(toolCall, "id")
    
    result = "Tool call request denied by user. Await further instructions."
    
    if allowed {
        if toolName == "run_shell_command" {
            result = agentRunShellCommand(tableGet(toolArgs, "command"))
        }
        if toolName == "find_file" {
            result = agentFindFile(tableGet(toolArgs, "pattern"))
        }
        if toolName == "search_workspace" {
            result = agentSearchWorkspace(tableGet(toolArgs, "pattern"))
        }
        if toolName == "read_file" {
            result = agentReadFile(getWorkspaceDir(), tableGet(toolArgs, "path"))
        }
        if toolName == "write_file" {
            result = agentFindFile(getWorkspaceDir(), tableGet(toolArgs, "file_path"), tableGet(toolArgs, "content"))
        }
        if toolName == "replace" {
            result = agentFindFile(getWorkspaceDir(), tableGet(toolArgs, "file_path"), tableGet(toolArgs, "old_string"), tableGet(toolArgs, "new_string"))
        }
    }
    
    message = createTable()
    tableSet(message, "role", "tool")
    tableSet(message, "content", result)
    tableSet(message, "name", toolName)
    tableSet(message, "tool_call_id", toolCallId)
    arrayPushBack(chatHistory, message)
    
    renderLLMChat()

    if arrayLen(pendingToolCalls) == 0 {
        requestChatCompletion()
    }
}

fn allowToolCall() { getToolCallResult(true) }
fn denyToolCall() { getToolCallResult(false) }

fn sendMessage() {
    if arrayLen(pendingToolCalls) == 0 {
        message = createTable()
        tableSet(message, "role", "user")
        tableSet(message, "content", chatInput)
        arrayPushBack(chatHistory, message)
        
        renderLLMChat()
    
        requestChatCompletion()
    }
}

fn llmChatInputUpdated() {
    chatInput = getBufferInput(llmChatId)
    renderLLMChat()
}

fn llmChatAddSpace() {
    chatInput = getBufferInput(llmChatId)
    chatInput = chatInput + " "
    setBufferInput(llmChatId, chatInput)
    renderLLMChat()
}

fn llmChatClearInput() {
    chatInput = ""
    setBufferInput(llmChatId, chatInput)
}

fn createLLMChat() {
    if llmChatId == -1 {
        bufferId = createSpecialBuffer("LLM Chat")
        llmChatId = bufferId
        registerBufferKeybind(bufferId, "nor enter", sendMessage)
        registerBufferKeybind(bufferId, "ins space", llmChatAddSpace)
        registerBufferKeybind(bufferId, "ins backspace", llmChatClearInput)
        registerBufferKeybind(bufferId, "nor y", allowToolCall)
        registerBufferKeybind(bufferId, "nor n", denyToolCall)
        registerBufferInputHook(bufferId, llmChatInputUpdated)
        renderLLMChat()
    }
    setActiveBuffer(llmChatId)
}

registerGlobalKeybind("nor space c", createLLMChat)

############

# Fuzzy Find File

fuzzyFileFinderId = -1
fuzzyFileFinderInput = ""
fuzzyFileFinderResults = createArray()
fuzzySelectedEntry = 0
fuzzyFileFinderPreviousBufferId = -1

fn renderFuzzyFileFinder() {
    content = "\n------\n" + fuzzyFileFinderInput + "\n------\n"
    i = 0
    loop {
        if i >= arrayLen(fuzzyFileFinderResults) { break }
        result = arrayGet(fuzzyFileFinderResults, i)
        if fuzzySelectedEntry == i {
            content = content + "\n > " + result            
        } if fuzzySelectedEntry != i {
            content = content + "\n   " + result 
        }
        i = i + 1
    }
    setBufferContent(fuzzyFileFinderId, content)
}

fn fuzzyFileFinderWorkspaceEntries() {
    results = runShellCommand("fd --type f --strip-cwd-prefix --full-path --absolute-path", getWorkspaceDir())
    fuzzyFileFinderResults = stringSplitLines(tableGet(results, "stdout"))
    fuzzySelectedEntry = 0
    renderFuzzyFileFinder()
}

fn fuzzyFileFinderInputUpdated() {
    fuzzyFileFinderInput = getBufferInput(fuzzyFileFinderId)
    results = runShellCommand("fd --type f --strip-cwd-prefix --full-path --absolute-path | fzf -f \"" + fuzzyFileFinderInput + "\"", getWorkspaceDir())
    fuzzyFileFinderResults = stringSplitLines(tableGet(results, "stdout"))
    fuzzySelectedEntry = 0
    renderFuzzyFileFinder()
}

fn fuzzyFileFinderAddSpace() {
    fuzzyFileFinderInput = getBufferInput(fuzzyFileFinderId)
    fuzzyFileFinderInput = fuzzyFileFinderInput + " "
    setBufferInput(fuzzyFileFinderId, fuzzyFileFinderInput)
    renderFuzzyFileFinder()
}

fn fuzzyFileFinderClearInput() {
    fuzzyFileFinderInput = ""
    setBufferInput(fuzzyFileFinderId, fuzzyFileFinderInput)
}

fn fuzzyFileFinderMoveSelection(delta) {
    if arrayLen(fuzzyFileFinderResults) == 0 {
        fuzzySelectedEntry = 0
        renderFuzzyFileFinder()
        return null
    }
    fuzzySelectedEntry = fuzzySelectedEntry + delta
    if fuzzySelectedEntry < 0 { fuzzySelectedEntry = arrayLen(fuzzyFileFinderResults) - 1 }
    if fuzzySelectedEntry >= arrayLen(fuzzyFileFinderResults) { fuzzySelectedEntry = 0 }
    renderFuzzyFileFinder()
}

fn fuzzyFileFinderNext() {
    fuzzyFileFinderMoveSelection(1)
}

fn fuzzyFileFinderPrevious() {
    fuzzyFileFinderMoveSelection(-1)
}

fn fuzzyFileFinderSelect() {
    if fuzzySelectedEntry < arrayLen(fuzzyFileFinderResults) {
        path = arrayGet(fuzzyFileFinderResults, fuzzySelectedEntry)
        openFile(path)
        runAction("quit-insert-mode")
    }
}

fn fuzzyFileFinderEscape() {
    if fuzzyFileFinderPreviousBufferId != -1 {
        setActiveBuffer(fuzzyFileFinderPreviousBufferId)
        runAction("quit-insert-mode")
    }
}

fn createFuzzyFileFinder() {
    fuzzyFileFinderPreviousBufferId = getActiveBuffer()
    if fuzzyFileFinderId == -1 {
        bufferId = createSpecialBuffer("Fuzzy File Finder")
        fuzzyFileFinderId = bufferId
        registerBufferKeybind(bufferId, "ins space", fuzzyFileFinderAddSpace)
        registerBufferKeybind(bufferId, "ins backspace", fuzzyFileFinderClearInput)
        registerBufferKeybind(bufferId, "ins tab", fuzzyFileFinderNext)
        registerBufferKeybind(bufferId, "ins down", fuzzyFileFinderNext)
        registerBufferKeybind(bufferId, "ins up", fuzzyFileFinderPrevious)
        registerBufferKeybind(bufferId, "ins enter", fuzzyFileFinderSelect)
        registerBufferKeybind(bufferId, "ins escape", fuzzyFileFinderEscape)
        registerBufferInputHook(bufferId, fuzzyFileFinderInputUpdated)
    }
    fuzzyFileFinderClearInput()
    setActiveBuffer(fuzzyFileFinderId)
    fuzzyFileFinderWorkspaceEntries()
    runAction("enter-insert-mode")
}

registerGlobalKeybind("nor space f", createFuzzyFileFinder)

############

# Switch Buffer

switchBufferId = -1
switchBufferInput = ""
switchBufferResults = createArray()
switchBufferBuffers = createArray()
switchBufferSelectedEntry = 0
switchBufferSelectedId = -1
switchBufferPreviousBufferId = -1
switchBufferInitialActiveId = -1

fn switchBufferDisplay(entry) {
    display = tableGet(entry, "display_name")
    if tableGet(entry, "special") {
        display = "[special] " + display
    }
    if tableGet(entry, "modified") {
        display = display + " *"
    }
    if tableGet(entry, "is_active") {
        display = display + " (active)"
    }
    return display
}

fn renderSwitchBuffer() {
    content = "\n------\n" + switchBufferInput + "\n------\n"
    if arrayLen(switchBufferResults) == 0 {
        content = content + "\n   No buffers"
    }
    i = 0
    loop {
        if i >= arrayLen(switchBufferResults) { break }
        entry = arrayGet(switchBufferResults, i)
        display = switchBufferDisplay(entry)
        if switchBufferSelectedEntry == i {
            content = content + "\n > " + display
        } if switchBufferSelectedEntry != i {
            content = content + "\n   " + display
        }
        i = i + 1
    }
    setBufferContent(switchBufferId, content)
}

fn switchBufferLoadBuffers() {
    buffers = listBuffers()
    switchBufferBuffers = fromJson(buffers)
}

fn switchBufferUpdateSelectionFromId(targetId) {
    if arrayLen(switchBufferResults) == 0 {
        switchBufferSelectedEntry = 0
        switchBufferSelectedId = -1
        renderSwitchBuffer()
        return null
    }

    switchBufferSelectedEntry = 0
    i = 0
    loop {
        if i >= arrayLen(switchBufferResults) { break }
        entry = arrayGet(switchBufferResults, i)
        if tableGet(entry, "id") == targetId {
            switchBufferSelectedEntry = i
            break
        }
        i = i + 1
    }

    selectedEntry = arrayGet(switchBufferResults, switchBufferSelectedEntry)
    switchBufferSelectedId = tableGet(selectedEntry, "id")
    renderSwitchBuffer()
}

fn switchBufferUpdateResults() {
    switchBufferLoadBuffers()

    previousSelectionId = switchBufferSelectedId
    if previousSelectionId == -1 {
        previousSelectionId = switchBufferInitialActiveId
    }

    query = stringToLower(switchBufferInput)
    switchBufferResults = createArray()

    i = 0
    loop {
        if i >= arrayLen(switchBufferBuffers) { break }
        entry = arrayGet(switchBufferBuffers, i)
        name = stringToLower(tableGet(entry, "display_name"))
        if stringLen(query) == 0 or stringContains(name, query) {
            arrayPushBack(switchBufferResults, entry)
        }
        i = i + 1
    }

    switchBufferSelectedId = -1
    switchBufferSelectedEntry = 0
    switchBufferUpdateSelectionFromId(previousSelectionId)
}

fn switchBufferInputUpdated() {
    switchBufferInput = getBufferInput(switchBufferId)
    switchBufferUpdateResults()
}

fn switchBufferAddSpace() {
    switchBufferInput = getBufferInput(switchBufferId)
    switchBufferInput = switchBufferInput + " "
    setBufferInput(switchBufferId, switchBufferInput)
    renderSwitchBuffer()
}

fn switchBufferClearInput() {
    switchBufferInput = ""
    setBufferInput(switchBufferId, switchBufferInput)
    renderSwitchBuffer()
}

fn switchBufferMoveSelection(delta) {
    if arrayLen(switchBufferResults) == 0 {
        switchBufferSelectedEntry = 0
        switchBufferSelectedId = -1
        renderSwitchBuffer()
        return null
    }
    switchBufferSelectedEntry = switchBufferSelectedEntry + delta
    if switchBufferSelectedEntry < 0 { switchBufferSelectedEntry = arrayLen(switchBufferResults) - 1 }
    if switchBufferSelectedEntry >= arrayLen(switchBufferResults) { switchBufferSelectedEntry = 0 }
    entry = arrayGet(switchBufferResults, switchBufferSelectedEntry)
    switchBufferSelectedId = tableGet(entry, "id")
    renderSwitchBuffer()
}

fn switchBufferNext() {
    switchBufferMoveSelection(1)
}

fn switchBufferPrevious() {
    switchBufferMoveSelection(-1)
}

fn switchBufferSelect() {
    if switchBufferSelectedEntry < arrayLen(switchBufferResults) {
        selection = arrayGet(switchBufferResults, switchBufferSelectedEntry)
        bufferId = tableGet(selection, "id")
        setActiveBuffer(bufferId)
        runAction("quit-insert-mode")
    }
}

fn switchBufferEscape() {
    if switchBufferPreviousBufferId != -1 {
        setActiveBuffer(switchBufferPreviousBufferId)
        runAction("quit-insert-mode")
    }
}

fn createSwitchBuffer() {
    switchBufferPreviousBufferId = getActiveBuffer()
    switchBufferInitialActiveId = switchBufferPreviousBufferId
    switchBufferSelectedId = -1

    if switchBufferId == -1 {
        bufferId = createSpecialBuffer("Switch Buffer")
        switchBufferId = bufferId
        registerBufferKeybind(bufferId, "ins space", switchBufferAddSpace)
        registerBufferKeybind(bufferId, "ins backspace", switchBufferClearInput)
        registerBufferKeybind(bufferId, "ins tab", switchBufferNext)
        registerBufferKeybind(bufferId, "ins down", switchBufferNext)
        registerBufferKeybind(bufferId, "ins up", switchBufferPrevious)
        registerBufferKeybind(bufferId, "ins enter", switchBufferSelect)
        registerBufferKeybind(bufferId, "ins escape", switchBufferEscape)
        registerBufferInputHook(bufferId, switchBufferInputUpdated)
    }

    switchBufferClearInput()
    switchBufferUpdateResults()
    setActiveBuffer(switchBufferId)
    runAction("enter-insert-mode")
}

registerGlobalKeybind("nor space b", createSwitchBuffer)

############

# Workspace Search

workspaceSearchId = -1
workspaceSearchInput = ""
workspaceSearchResults = createArray()
workspaceSearchSelectedEntry = 0
workspaceSearchPreviousBufferId = -1

fn renderWorkspaceSearch() {
    content = "\n------\n" + workspaceSearchInput + "\n------\n"
    if arrayLen(workspaceSearchResults) == 0 {
        content = content + "\n   No matches"
    }
    i = 0
    loop {
        if i >= arrayLen(workspaceSearchResults) { break }
        result = arrayGet(workspaceSearchResults, i)
        display = tableGet(result, "display")
        if workspaceSearchSelectedEntry == i {
            content = content + "\n > " + display
        } if workspaceSearchSelectedEntry != i {
            content = content + "\n   " + display
        }
        i = i + 1
    }
    setBufferContent(workspaceSearchId, content)
}

fn workspaceSearchClearInput() {
    workspaceSearchInput = ""
    setBufferInput(workspaceSearchId, workspaceSearchInput)
}

fn workspaceSearchUpdateResults() {
    workspaceSearchResults = createArray()
    if stringLen(workspaceSearchInput) <= 1 {
        workspaceSearchSelectedEntry = 0
        renderWorkspaceSearch()
        return null
    }

    results = runShellCommand("rg --json --color=never --smart-case --max-count 100 \"" + workspaceSearchInput + "\"", getWorkspaceDir())
    lines = stringSplitLines(tableGet(results, "stdout"))

    i = 0
    loop {
        if i >= arrayLen(lines) { break }
        line = arrayGet(lines, i)
        if line != "" {
            rgResult = fromJson(line)
            if tableGet(rgResult, "type") == "match" {
                data = tableGet(rgResult, "data")
                filePath = tableGet(tableGet(data, "path"), "text")
                row = tableGet(data, "line_number") - 1
                lineText = tableGet(tableGet(data, "lines"), "text")
                lineParts = stringSplitLines(lineText)
                if arrayLen(lineParts) > 0 {
                    lineText = arrayGet(lineParts, 0)
                }
                submatches = tableGet(data, "submatches")
                j = 0
                loop {
                    if j >= arrayLen(submatches) { break }
                    submatch = arrayGet(submatches, j)
                    start = tableGet(submatch, "start")
                    end = tableGet(submatch, "end")

                    display = filePath + ":" + toJson(row) + ":" + toJson(start) + " " + lineText

                    entry = createTable()
                    tableSet(entry, "file_path", filePath)
                    tableSet(entry, "row", row)
                    tableSet(entry, "start", start)
                    tableSet(entry, "end", end)
                    tableSet(entry, "display", display)
                    arrayPushBack(workspaceSearchResults, entry)
                    j = j + 1
                }
            }
        }
        i = i + 1
    }
    workspaceSearchSelectedEntry = 0
    renderWorkspaceSearch()
}

fn workspaceSearchInputUpdated() {
    workspaceSearchInput = getBufferInput(workspaceSearchId)
    workspaceSearchUpdateResults()
}

fn workspaceSearchAddSpace() {
    workspaceSearchInput = getBufferInput(workspaceSearchId)
    workspaceSearchInput = workspaceSearchInput + " "
    setBufferInput(workspaceSearchId, workspaceSearchInput)
    renderWorkspaceSearch()
}

fn workspaceSearchMoveSelection(delta) {
    if arrayLen(workspaceSearchResults) == 0 {
        workspaceSearchSelectedEntry = 0
        renderWorkspaceSearch()
        return null
    }
    workspaceSearchSelectedEntry = workspaceSearchSelectedEntry + delta
    if workspaceSearchSelectedEntry < 0 { workspaceSearchSelectedEntry = arrayLen(workspaceSearchResults) - 1 }
    if workspaceSearchSelectedEntry >= arrayLen(workspaceSearchResults) { workspaceSearchSelectedEntry = 0 }
    renderWorkspaceSearch()
}

fn workspaceSearchNext() {
    workspaceSearchMoveSelection(1)
}

fn workspaceSearchPrevious() {
    workspaceSearchMoveSelection(-1)
}

fn workspaceSearchSelect() {
    if workspaceSearchSelectedEntry < arrayLen(workspaceSearchResults) {
        selection = arrayGet(workspaceSearchResults, workspaceSearchSelectedEntry)
        path = tableGet(selection, "file_path")
        row = tableGet(selection, "row")
        start = tableGet(selection, "start")
        end = tableGet(selection, "end")

        openFile(path)
        runAction("quit-insert-mode")
        runAction("go-to-buffer-start")

        i = 0
        loop {
            if i >= row { break }
            runAction("move-cursor-down")
            i = i + 1
        }

        runAction("move-cursor-line-start")

        j = 0
        loop {
            if j >= start { break }
            runAction("move-cursor-right")
            j = j + 1
        }

        length = end - start
        k = 0
        loop {
            if k >= length { break }
            runAction("extend-cursor-right")
            k = k + 1
        }

        runAction("quit-insert-mode")
    }
}

fn workspaceSearchEscape() {
    if workspaceSearchPreviousBufferId != -1 {
        setActiveBuffer(workspaceSearchPreviousBufferId)
        runAction("quit-insert-mode")
    }
}

fn createWorkspaceSearch() {
    workspaceSearchPreviousBufferId = getActiveBuffer()
    if workspaceSearchId == -1 {
        bufferId = createSpecialBuffer("Workspace Search")
        workspaceSearchId = bufferId
        registerBufferKeybind(bufferId, "ins space", workspaceSearchAddSpace)
        registerBufferKeybind(bufferId, "ins backspace", workspaceSearchClearInput)
        registerBufferKeybind(bufferId, "ins tab", workspaceSearchNext)
        registerBufferKeybind(bufferId, "ins down", workspaceSearchNext)
        registerBufferKeybind(bufferId, "ins up", workspaceSearchPrevious)
        registerBufferKeybind(bufferId, "ins enter", workspaceSearchSelect)
        registerBufferKeybind(bufferId, "ins escape", workspaceSearchEscape)
        registerBufferInputHook(bufferId, workspaceSearchInputUpdated)
    }
    workspaceSearchClearInput()
    setActiveBuffer(workspaceSearchId)
    workspaceSearchUpdateResults()
    runAction("enter-insert-mode")
}
